<!DOCTYPE html>
<html lang="en">

<head>
    <meta charset="UTF-8">
    <meta http-equiv="X-UA-Compatible" content="IE=edge">
    <meta name="viewport" content="width=device-width, initial-scale=1.0">
    <title>Stable Fluids</title>
    <link rel="stylesheet" href="style.css">
</head>

<body>
    <div class="background"></div>
    <canvas id="gl"></canvas>
</body>

</html>
<script id="vs" type="x-shader/x-vertex">#version 300 es
precision highp float;
in vec2 position;
in vec2 a_texCoord;
uniform float isFlip;

out vec2 uv;

void main() {
    gl_Position = vec4((position * 2.0 - 1.0) * vec2(1, isFlip), 0.0, 1.0);
    uv = a_texCoord;
}
</script>

<script id="init_fs" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    uniform vec2 resolution;
    uniform sampler2D calcTex, calcTex2;
    
    in vec2 uv;
    out vec4 outColor;
    
    void main() {
        outColor = vec4(uv, 1.0 - uv.y, 1.0);
    }
</script>

<script id="force_fs" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    uniform float dt;
    uniform vec2 resolution;
    uniform vec4 mouse;
    uniform sampler2D calcTex;

    in vec2 uv;
    out vec4 outColor;

    float box(vec2 p, vec2 b) {
        vec2 d = abs(p) - b;
        return length(max(d, 0.0)) + min(max(d.x, d.y), 0.0);
    }

    void main() {
        vec2 p = (gl_FragCoord.xy * 2.0 - resolution) / min(resolution.x, resolution.y);
        vec4 data = texture(calcTex, uv);
        float d = (1.0 - min(length(mouse.xy - p) * 3.0, 1.0)) * 2.0;
        d *= d;
        outColor = vec4(data.xy + mouse.zw * d, data.zw);
    }
</script>

<script id="velocity_fs" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    uniform float dt;
    uniform vec2 resolution;
    uniform sampler2D calcTex;

    in vec2 uv;
    out vec4 outColor;

    void main() {
        vec2 p = max(resolution.x, resolution.y) / resolution;
        vec4 data = texture(calcTex, uv);
        vec2 vel_old = data.xy;
        vec2 spot_old = uv - vel_old * dt * p;
        vec2 vel_new1 = texture(calcTex, spot_old).xy;
        vec2 uv2 = spot_old + vel_new1 * dt * p;
        vec2 error = uv2 - uv;
        vec2 uv3 = uv - error / 2.0;
        vec2 vel_2 = texture(calcTex, uv3).xy;
        vec2 spot_old2 = uv3 - vel_2 * dt * p;
        vec2 newVel2 = texture(calcTex, spot_old2).xy; 
        outColor = vec4(newVel2, data.zw);
    }
</script>

<script id="divergence_fs" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    uniform float dt;
    uniform vec2 resolution;
    uniform sampler2D calcTex;

    in vec2 uv;
    out vec4 outColor;

    void main() {
        vec2 pixel = 1.0 / resolution;
        float x0 = texture(calcTex, uv - vec2(pixel.x, 0)).x;
        float x1 = texture(calcTex, uv + vec2(pixel.x, 0)).x;
        float y0 = texture(calcTex, uv - vec2(0, pixel.y)).y;
        float y1 = texture(calcTex, uv + vec2(0, pixel.y)).y;
        float div = (x1 - x0 + y1 - y0) / 2.0;
        vec4 data = texture(calcTex, uv);

        outColor = vec4(data.xyz, div / dt);
    }
</script>

<script id="poisson_fs" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    uniform float dt;
    uniform vec2 resolution;
    uniform sampler2D calcTex;

    in vec2 uv;
    out vec4 outColor;

    void main() {
        vec2 pixel = 1.0 / resolution;
        float p0 = texture(calcTex, uv + vec2(pixel.x * 2.0, 0)).z;
        float p1 = texture(calcTex, uv - vec2(pixel.x * 2.0, 0)).z;
        float p2 = texture(calcTex, uv + vec2(0, pixel.y * 2.0)).z;
        float p3 = texture(calcTex, uv - vec2(0, pixel.y * 2.0)).z;
        vec4 data = texture(calcTex, uv);
        float div = data.w;
        float newP = (p0 + p1 + p2 + p3) / 4.0 - div;
        outColor = vec4(data.xy, newP, data.w);
    }
</script>

<script id="pressure_fs" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    uniform float dt;
    uniform vec2 resolution;
    uniform sampler2D calcTex, calcTex2;

    in vec2 uv;
    out vec4 outColor;

    void main() {
        float step = 1.0;
        vec2 pixel = 1.0 / resolution;

        float p0 = texture(calcTex, uv + vec2(pixel.x * step, 0)).z;
        float p1 = texture(calcTex, uv - vec2(pixel.x * step, 0)).z;
        float p2 = texture(calcTex, uv + vec2(0, pixel.y * step)).z;
        float p3 = texture(calcTex, uv - vec2(0, pixel.y * step)).z;
    
        vec4 data = texture(calcTex, uv);
        float w = texture(calcTex2, uv).x;
        vec2 v = data.xy;
        vec2 gradP = vec2(p0 - p1, p2 - p3) / 2.0;
        v = v - dt * gradP;
        outColor = vec4(v * w, data.zw);
    }
</script>

<script id="output_fs" type="x-shader/x-fragment">#version 300 es
    precision highp float;
    uniform float dt;
    uniform vec2 resolution;
    uniform sampler2D calcTex;

    in vec2 uv;
    out vec4 outColor;

    vec3 hsv2rgb(float h, float s, float v) {
        return ((clamp(abs(fract(h+vec3(0,2,1)/3.)*6.-3.)-1.,0.,1.)-1.)*s+1.)*v;
    }

    float box(vec2 p, vec2 b) {
        vec2 d = abs(p) - b;
        return length(max(d, 0.0)) + min(max(d.x, d.y), 0.0);
    }

    void main() {
        vec4 data = texture(calcTex, uv);
        vec2 vel = data.xy;
        float len = length(vel);
        vel = vel * 0.5 + 0.5;
        vec3 color = mix(vec3(0.0), vec3(0.2, 1.0, 0.85) * (0.5 + 0.5 * vec3(vel, 1.0)), len);
        outColor = vec4(color, 1.0);
    }
</script>

<script src="script.js"></script>